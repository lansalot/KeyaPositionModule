/*
   UDP Autosteer code for Teensy 4.1
   For AgOpenGPS
   01 Feb 2022
   Like all Arduino code - copied from somewhere else :)
   So don't claim it as your own
*/

////////////////// User Settings /////////////////////////
float fRoll = 0, fPitch = 0;
bool rpInitialized = false;

// How many degrees before decreasing Max PWM
#define LOW_HIGH_DEGREES 3.0

// -----------------------------------------------------------------------
// HEADING SOURCE SELECTION FOR AUTO-ZERO WAS
// Default values - can be modified via web interface or serial menu
// -----------------------------------------------------------------------

/*  PWM Frequency ->
   490hz (default) = 0
   122hz = 1
   3921hz = 2
*/
#define PWM_Frequency 0

/////////////////////////////////////////////

// if not in eeprom, overwrite
#define EEP_Ident 2484

//--------------------------- Switch Input Pins ------------------------
#define STEERSW_PIN 4
#define WORKSW_PIN 34
const uint8_t WAS_SENSOR_PIN = A15;

#define CONST_180_DIVIDED_BY_PI 57.2957795130823

#include <Wire.h>
#include <EEPROM.h>

#include <IPAddress.h>
#include "BNO08x_AOG.h"
#include "KeyaPositionModule.h"

#ifdef ARDUINO_TEENSY41
// ethernet
#include <NativeEthernet.h>
#include <NativeEthernetUdp.h>
#endif

#ifdef ARDUINO_TEENSY41
// uint8_t Ethernet::buffer[200]; // udp send and receive buffer
uint8_t autoSteerUdpData[UDP_TX_PACKET_MAX_SIZE]; // Buffer For Receiving UDP Data
#endif

extern AutoZeroParams azParams;

// loop time variables in microseconds
const uint16_t LOOP_TIME = 25; // 40Hz
uint32_t autsteerLastTime = LOOP_TIME;
uint32_t currentTime = LOOP_TIME;

const uint16_t WATCHDOG_THRESHOLD = 100;
const uint16_t WATCHDOG_FORCE_VALUE = WATCHDOG_THRESHOLD + 2; // Should be greater than WATCHDOG_THRESHOLD
uint8_t watchdogTimer = WATCHDOG_FORCE_VALUE;
uint8_t watchdogKeya = 0;

// Heart beat hello AgIO
uint8_t helloFromIMU[] = {128, 129, 121, 121, 5, 0, 0, 0, 0, 0, 71};
uint8_t helloFromAutoSteer[] = {0x80, 0x81, 126, 126, 5, 0, 0, 0, 0, 0, 71};

// Hello from Machine - PGN 123 for detection by AgIO
uint8_t helloFromMachine[] = {128, 129, 123, 123, 5, 0, 0, 0, 0, 0, 71};
int16_t helloSteerPosition = 0;

// fromAutoSteerData FD 253 - ActualSteerAngle*100 -5,6, SwitchByte-7, pwmDisplay-8
uint8_t PGN_253[] = {0x80, 0x81, 126, 0xFD, 8, 0, 0, 0, 0, 0, 0, 0, 0, 0xCC};
int8_t PGN_253_Size = sizeof(PGN_253) - 1;

// fromAutoSteerData FD 250 - sensor values etc
uint8_t PGN_250[] = {0x80, 0x81, 126, 0xFA, 8, 0, 0, 0, 0, 0, 0, 0, 0, 0xCC};
int8_t PGN_250_Size = sizeof(PGN_250) - 1;
uint8_t aog2Count = 0;
float sensorReading;
float sensorSample;

elapsedMillis gpsSpeedUpdateTimer = 0;

// EEPROM
int16_t EEread = 0;

// Relays
bool isRelayActiveHigh = true;
uint8_t relay = 0, relayHi = 0, uTurn = 0;
uint8_t tram = 0;
bool isPGNFound = false, isHeaderFound = false;
uint8_t pgn = 0, dataLength = 0, idx = 0;
boolean goDown = false, endDown = false, bitState = false, bitStateOld = false; // CAN Hitch Control
byte hydLift = 0;
byte goPress[8] = {0x15, 0x33, 0x1E, 0xCA, 0x80, 0x01, 0x00, 0x00};  //  press big go
byte goLift[8] = {0x15, 0x33, 0x1E, 0xCA, 0x00, 0x02, 0x00, 0x00};   //  lift big go
byte endPress[8] = {0x15, 0x34, 0x1E, 0xCA, 0x80, 0x03, 0x00, 0x00}; //  press big end
byte endLift[8] = {0x15, 0x34, 0x1E, 0xCA, 0x00, 0x04, 0x00, 0x00};  //  lift big end

// Switches
uint8_t remoteSwitch = 0, workSwitch = 0, steerSwitch = 1, switchByte = 0;

// On Off
uint8_t guidanceStatus = 0;
uint8_t prevGuidanceStatus = 0;
bool guidanceStatusChanged = false;

// CAN Bus
bool engageCAN = false; // Variable for Engage from CAN
bool workCAN = false;
long unsigned int lastIdActive = 0;
uint8_t KBUSRearHitch = 250; // Variable for hitch height from KBUS (0-250 *0.4 = 0-100%) - CaseIH tractor bus

uint8_t countHyd = 0; // Counter for Fendt button hold time

uint32_t myTime;
uint32_t lastpush;
uint32_t Time;
uint32_t relayTime;

uint8_t lastHydLift = 0;
uint32_t lastpushbutton = 0;

// speed sent as *10
float gpsSpeed = 0;

// steering variables
float steerAngleActual = 0;
float steerAngleSetPoint = 0; // the desired angle from AgOpen
int16_t steeringPosition = 0; // from steering sensor
float steerAngleError = 0;    // setpoint - actual

// pwm variables
int16_t pwmDrive = 0, pwmDisplay = 0;
float pValue = 0;
float errorAbs = 0;
float highLowPerDeg = 0;

// Steer switch button  ***********************************************************************************************************
uint8_t currentState = 1, reading, previous = 0;
uint8_t pulseCount = 0; // Steering Wheel Encoder
bool encEnable = false; // debounce flag
uint8_t thisEnc = 0, lastEnc = 0;

// Variables for settings
struct Storage
{
  uint8_t Kp = 40;     // proportional gain
  uint8_t lowPWM = 10; // band of no action
  int16_t wasOffset = 0;
  uint8_t minPWM = 9;
  uint8_t highPWM = 60; // max PWM value
  float steerSensorCounts = 30;
  float AckermanFix = 1; // sent as percent
};
Storage steerSettings; // 11 bytes (AW: 14 surely?

// Variables for config - 0 is false
struct Config
{
  uint8_t raiseTime = 2;
  uint8_t lowerTime = 4;
  uint8_t enableToolLift = 0;
  uint8_t isRelayActiveHigh = 0; // if zero, active low (default)

  uint8_t user1 = 0; // user defined values set in machine tab
  uint8_t user2 = 0;
  uint8_t user3 = 0;
  uint8_t user4 = 0;
};
Config aogConfig; // 4 bytes

float azCorrAccum = 0.0f; // sub-tick accumulation for gradual correction
KeyaAutoZeroRuntime keyaAutoZeroRuntime;
KeyaAutoZeroConfig keyaAutoZeroConfig = keyaDefaultAutoZeroConfig();

void steerSettingsInit()
{
  // for PWM High to Low interpolator
  highLowPerDeg = ((float)(steerSettings.highPWM - steerSettings.lowPWM)) / LOW_HIGH_DEGREES;
}

void autosteerSetup()
{
  // keep pulled high and drag low to activate, noise free safe
  pinMode(WORKSW_PIN, INPUT_PULLUP);
  pinMode(STEERSW_PIN, INPUT_PULLUP);

  EEPROM.get(0, EEread); // read identifier

  if (EEread != EEP_Ident) // check on first start and write EEPROM
  {
    EEPROM.put(0, EEP_Ident);
    EEPROM.put(10, steerSettings);
    EEPROM.put(40, steerConfig);
    EEPROM.put(60, networkAddress);
    EEPROM.get(70, aogConfig);
  }
  else
  {
    EEPROM.get(10, steerSettings); // read the Settings
    EEPROM.get(40, steerConfig);
    EEPROM.get(60, networkAddress);
    EEPROM.get(70, aogConfig);
  }

  steerSettingsInit();
  if (sectionout == 1)
  {
    sectionControlSetup(); // added by FlorianT
  }

  // Restore saved WAS offset between restarts
  {
    float savedOffset = 0.0f;
    EEPROM.get(EEPROM_ADDR_WAS_OFFSET_F, savedOffset);
    if (!isnan(savedOffset) && !isinf(savedOffset) && fabsf(savedOffset) < 30.0f)
      wasOffsetF = savedOffset;
    else
      wasOffsetF = 0.0f;
    steerSettings.wasOffset = (int16_t)round(wasOffsetF * steerSettings.steerSensorCounts);
    Serial.print("WAS auto-zero offset restored: ");
    Serial.println(wasOffsetF, 3);
  }

  // Restore Keya ticks/degree ratio (mechanical calibration)
  {
    keyaLoadTicksPerDeg(EEPROM_ADDR_KEYA_TICKS, keyaTicksPerDeg, KEYA_TICKS_PER_DEG_DEFAULT);
    Serial.print("Keya ticks/deg: ");
    Serial.println(keyaTicksPerDeg, 1);
  }
  wasZeroDone = false; // zero must be established at every startup

  if (Autosteer_running)
  {
    Serial.println("Autosteer running, waiting for AgOpenGPS");
  }
  else
    Serial.println("Setting up WAS ADC");
  analogReadResolution(12);
  analogReadAveraging(16);
  pinMode(WAS_SENSOR_PIN, INPUT_DISABLE);

  // Load auto-zero parameters from EEPROM
  azMenuSetup();
  emaParamsLoad();
  keyaInitAutoZeroRuntime(keyaAutoZeroRuntime);

} // End of Setup

void autosteerLoop()
{
#ifdef ARDUINO_TEENSY41
  ReceiveUdp();
#endif

  // Serial menu for auto-zero parameters (press 'm' in serial monitor)
  if (azMenuLoop())
    return;

  // Serial.println("AutoSteer loop");

  // Loop triggers every 100 msec and sends back gyro heading, and roll, steer angle etc
  currentTime = systick_millis_count;

  if (currentTime - autsteerLastTime >= LOOP_TIME)
  {
    autsteerLastTime = currentTime;

    // reset debounce
    encEnable = true;

    // If connection lost to AgOpenGPS, the watchdog will count up and turn off steering
    if (watchdogTimer++ > 250)
      watchdogTimer = WATCHDOG_FORCE_VALUE;

    if (watchdogKeya++ > 200)
    {
      watchdogKeya = 0;
      // digitalWrite(PWM2_RPWM, LOW);
    }

    // read all the switches
    workSwitch = digitalRead(WORKSW_PIN); // read work switch
    if (workCAN == 1)
      workSwitch = 0; // If CAN workswitch is on, set workSwitch ON

    if (steerConfig.SteerSwitch == 1) // steer switch on - off
    {
      steerSwitch = digitalRead(STEERSW_PIN); // read auto steer enable switch open = 0n closed = Off
    }
    else if (steerConfig.SteerButton == 1) // steer Button momentary
    {
      reading = digitalRead(STEERSW_PIN);
      if (engageCAN)
      {
        reading = LOW;     // CAN Engage is ON (Button is Pressed)
        engageCAN = false; // mod test thibault
      }
      if (reading == LOW && previous == HIGH)
      {
        if (currentState == 1)
        {
          currentState = 0;
          steerSwitch = 0;
        }
        else
        {
          currentState = 1;
          steerSwitch = 1;
        }
      }
      previous = reading;
    }
    else // No steer switch and no steer button
    {
      // So set the correct value. When guidanceStatus = 1,
      // it should be on because the button is pressed in the GUI
      // But the guidancestatus should have set it off first
      if (guidanceStatusChanged && guidanceStatus == 1 && steerSwitch == 1 && previous == 0)
      {
        steerSwitch = 0;
        previous = 1;
      }

      // This will set steerswitch off and make the above check wait until the guidanceStatus has gone to 0
      if (guidanceStatusChanged && guidanceStatus == 0 && steerSwitch == 0 && previous == 1)
      {
        steerSwitch = 1;
        previous = 0;
      }
    }

    if (steerConfig.ShaftEncoder && pulseCount >= steerConfig.PulseCountMax)
    {
      steerSwitch = 1; // reset values like it turned off
      currentState = 1;
      previous = 0;
    }

    // Current sensor?
    if (steerConfig.CurrentSensor)
    {
      sensorReading = KeyaCurrentSensorReading;
      if (KeyaCurrentSensorReading >= steerConfig.PulseCountMax)
      {
        steerSwitch = 1; // reset values like it turned off
        currentState = 1;
        previous = 0;
        engageCAN = false;
      }
    }

    switchByte = 0;
    switchByte |= (steerSwitch << 2);
    switchByte |= (steerSwitch << 1); // put steerswitch status in bit 1 position
    switchByte |= workSwitch;

    // =================================================================
    // STEERING ANGLE CALCULATION
    //   IsDanfoss = 1 -> Keya, + auto-zero
    //   IsDanfoss = 0 -> physical WAS
    // =================================================================
    if (steerConfig.IsDanfoss)
    {
      // --- KEYA ENCODER MODE ---
      int32_t deltaTicks = keyaComputeDeltaTicks(keyaEncoderRaw, keyaZeroTicks);
      float rawAngle = keyaComputeAngleDeg(keyaEncoderRaw,
                                           keyaZeroTicks,
                                           keyaTicksPerDeg,
                                           steerConfig.InvertWAS != 0);

      steerAngleActual = rawAngle;
      helloSteerPosition = (int16_t)(rawAngle * 100.0f);
      steeringPosition = (int16_t)deltaTicks;

      // Block autoguiding until zero is established
      if (!wasZeroDone)
        watchdogTimer = WATCHDOG_FORCE_VALUE;
    }
    else
    {
      // get steering position
      float curPos = (float)analogRead(WAS_SENSOR_PIN);
      // apparently we should be around 13610 for the old-style ADC reading at 5v, but it had an amplifier hence that 13610 target
      // Teensy ADC has no such amplifier, so let's change 3.23 to 4.35
      steeringPosition = int(curPos * 4.35);
      // Serial.println("RawPosition: " + String(curPos) + " Corrected: " + String(steeringPosition));

      // at 15cm, the raw range is closed around 3216 raw, 10100 cooked (closed) to open at 1 raw and 3 cooked
      // middle is around 1300
      // DETERMINE ACTUAL STEERING POSITION

      // convert position to steer angle. 32 counts per degree of steer pot position in my case
      //  ***** make sure that negative steer angle makes a left turn and positive value is a right turn *****
      // Approx CPD 130/ Ack 96 with LR WAS sensor  (ballpark figure)
      if (steerConfig.InvertWAS)
      {
        steeringPosition = (steeringPosition - 6805 - steerSettings.wasOffset); // 1/2 of full scale
        steerAngleActual = (float)(steeringPosition) / -steerSettings.steerSensorCounts;
      }
      else
      {
        steeringPosition = (steeringPosition - 6805 + steerSettings.wasOffset); // 1/2 of full scale
        steerAngleActual = (float)(steeringPosition) / steerSettings.steerSensorCounts;
      }
    }

    // Ackerman fix
    if (steerAngleActual < 0)
      steerAngleActual = (steerAngleActual * steerSettings.AckermanFix);

    // =================================================================
    // KEYA ENCODER AUTO-ZERO - only in IsDanfoss = 1 mode
    // =================================================================
    if (steerConfig.IsDanfoss)
    {
      int32_t oldZeroTicks = keyaZeroTicks;
      bool oldZeroDone = wasZeroDone;

      KeyaAutoZeroInput azIn = {
          .nowMs = millis(),
          .gpsSpeedKmh = gpsSpeed,
          .yawDeg = yaw,
          .emaGpsHeadingX10Deg = emaGpsHdg,
          .steerAngleActualDeg = steerAngleActual,
          .invertWas = steerConfig.InvertWAS != 0,
          .guidanceActive = watchdogTimer < WATCHDOG_THRESHOLD};

      bool azCommitted = keyaUpdateAutoZero(azIn,
                                            azParams,
                                            keyaAutoZeroConfig,
                                            keyaEncoderRaw,
                                            keyaTicksPerDeg,
                                            keyaZeroTicks,
                                            wasZeroDone,
                                            keyaAutoZeroRuntime);

      azCorrAccum = keyaAutoZeroRuntime.corrAccum;

      if (azCommitted)
      {
        if (!oldZeroDone && wasZeroDone)
        {
          Serial.print("[AZ] First zero established: ");
          Serial.println(keyaZeroTicks);
        }
        else
        {
          Serial.print(azIn.guidanceActive ? "[AZ-PRECIS] " : "[AZ-RAPIDE] ");
          Serial.print("zero update: ");
          Serial.print(oldZeroTicks);
          Serial.print(" -> ");
          Serial.println(keyaZeroTicks);
        }
      }
    } // end if (steerConfig.IsDanfoss) auto-zero
    // =================================================================

    if (watchdogTimer < WATCHDOG_THRESHOLD)
    {
      steerAngleError = steerAngleActual - steerAngleSetPoint; // calculate the steering error
      // if (abs(steerAngleError)< steerSettings.lowPWM) steerAngleError = 0;

      calcSteeringPID(); // do the pid
      motorDrive();      // out to motors the pwm value
    }
    else
    {
      pwmDrive = 0;       // turn off steering motor
      disableKeyaSteer(); // If we lost the connection to AOG, definitely disable steering
      motorDrive();       // out to motors the pwm value
      pulseCount = 0;
    }
    if (Brand == 3)
      SetRelaysFendt();
  } // end of timed loop

  // This runs continuously, outside of the timed loop, keeps checking for new udpData, turn sense
  // delay(1);

  // Speed pulse
  if (gpsSpeedUpdateTimer < 1000)
  {
    if (speedPulseUpdateTimer > 200)
    {
      speedPulseUpdateTimer = 0;

      // gpsSpeed in km/h → m/s = /3.6 → * imp/m = frequency Hz
      float speedPulse = (gpsSpeed / 3.6f) * SPEED_PULSE_IMP_PER_METER;

      if (gpsSpeed > 0.11)
      {
        tone(velocityPWM_Pin, uint16_t(speedPulse));
      }
      else
      {
        noTone(velocityPWM_Pin);
      }
    }
  }

  if (sectionout == 1)
  {
    sectionControlLoop(); // added by FlorianT
  }

} // end of main loop

int currentRoll = 0;
int rollLeft = 0;
int steerLeft = 0;

#ifdef ARDUINO_TEENSY41
// UDP Receive
void ReceiveUdp()
{
  // When ethernet is not running, return directly. parsePacket() will block when we don't
  if (!Ethernet_running)
  {
    return;
  }

  uint16_t len = Eth_udpAutoSteer.parsePacket();

  // if (len > 0)
  // {
  //  Serial.print("ReceiveUdp: ");
  //  Serial.println(len);
  // }

  // Check for len > 4, because we check byte 0, 1, 3 and 3
  if (len > 4)
  {
    Eth_udpAutoSteer.read(autoSteerUdpData, UDP_TX_PACKET_MAX_SIZE);

    if (autoSteerUdpData[0] == 0x80 && autoSteerUdpData[1] == 0x81 && autoSteerUdpData[2] == 0x7F) // Data
    {
      if (autoSteerUdpData[3] == 0xFE && Autosteer_running) // 254
      {
        gpsSpeed = ((float)(autoSteerUdpData[5] | autoSteerUdpData[6] << 8)) * 0.1;
        gpsSpeedUpdateTimer = 0;

        prevGuidanceStatus = guidanceStatus;

        guidanceStatus = autoSteerUdpData[7];
        guidanceStatusChanged = (guidanceStatus != prevGuidanceStatus);

        // Bit 8,9    set point steer angle * 100 is sent
        steerAngleSetPoint = ((float)(autoSteerUdpData[8] | ((int8_t)autoSteerUdpData[9]) << 8)) * 0.01; // high low bytes

        // Serial.print("steerAngleSetPoint: ");
        // Serial.println(steerAngleSetPoint);

        // Serial.println(gpsSpeed);

        if ((bitRead(guidanceStatus, 0) == 0) || (gpsSpeed < 0.1) || (steerSwitch == 1))
        {
          watchdogTimer = WATCHDOG_FORCE_VALUE; // turn off steering motor
        }
        else // valid conditions to turn on autosteer
        {
          watchdogTimer = 0; // reset watchdog
        }

        //----------------------------------------------------------------------------
        // Serial Send to agopenGPS

        int16_t sa = (int16_t)(steerAngleActual * 100);

        PGN_253[5] = (uint8_t)sa;
        PGN_253[6] = sa >> 8;

        // heading and roll: sentinel values 9999/8888
        // BNO from this sketch is sent via helloFromIMU (PGN 121)
        // and not via PGN 253 – same behaviour as original
        PGN_253[7] = (uint8_t)9999;
        PGN_253[8] = 9999 >> 8;
        PGN_253[9] = (uint8_t)8888;
        PGN_253[10] = 8888 >> 8;

        PGN_253[11] = switchByte;
        PGN_253[12] = (uint8_t)pwmDisplay;

        // checksum
        int16_t CK_A = 0;
        for (uint8_t i = 2; i < PGN_253_Size; i++)
          CK_A = (CK_A + PGN_253[i]);

        PGN_253[PGN_253_Size] = CK_A;

        // off to AOG
        SendUdp(PGN_253, sizeof(PGN_253), Eth_ipDestination, portDestination);

        // Steer Data 2 -------------------------------------------------
        if (steerConfig.PressureSensor || steerConfig.CurrentSensor)
        {
          if (aog2Count++ > 2)
          {
            // Send fromAutosteer2
            PGN_250[5] = (byte)sensorReading;

            // add the checksum for AOG2
            CK_A = 0;

            for (uint8_t i = 2; i < PGN_250_Size; i++)
            {
              CK_A = (CK_A + PGN_250[i]);
            }

            PGN_250[PGN_250_Size] = CK_A;

            // off to AOG
            SendUdp(PGN_250, sizeof(PGN_250), Eth_ipDestination, portDestination);
            aog2Count = 0;
          }
        }

        // Serial.println(steerAngleActual);
        //--------------------------------------------------------------------------
      }

      // steer settings
      else if (autoSteerUdpData[3] == 0xFC && Autosteer_running) // 252
      {
        // PID values
        steerSettings.Kp = ((float)autoSteerUdpData[5]); // read Kp from AgOpenGPS

        steerSettings.highPWM = autoSteerUdpData[6]; // read high pwm

        steerSettings.lowPWM = (float)autoSteerUdpData[7]; // read lowPWM from AgOpenGPS

        steerSettings.minPWM = autoSteerUdpData[8]; // read the minimum amount of PWM for instant on

        float temp = (float)steerSettings.minPWM * 1.2;
        steerSettings.lowPWM = (byte)temp;

        steerSettings.steerSensorCounts = autoSteerUdpData[9]; // sent as setting displayed in AOG

        // In Keya encoder mode: steerSensorCounts is used as calibration multiplier
        if (steerConfig.IsDanfoss)
        {
          keyaApplyAogSteerSensorCounts(steerSettings.steerSensorCounts,
                                        KEYA_TICKS_PER_DEG_DEFAULT,
                                        keyaTicksPerDeg);
          keyaSaveTicksPerDeg(EEPROM_ADDR_KEYA_TICKS,
                              keyaTicksPerDeg,
                              KEYA_TICKS_PER_DEG_DEFAULT);
        }

        steerSettings.wasOffset = (autoSteerUdpData[10]); // read was zero offset Lo

        steerSettings.wasOffset |= (autoSteerUdpData[11] << 8); // read was zero offset Hi

        steerSettings.AckermanFix = (float)autoSteerUdpData[12] * 0.01;

        // crc
        // autoSteerUdpData[13];

        // store in EEPROM
        EEPROM.put(10, steerSettings);

        // In Keya mode: if AOG sends wasOffset = 0 -> reset encoder zero
        if (steerConfig.IsDanfoss && steerSettings.wasOffset == 0)
        {
          keyaForceZeroAtCurrentEncoder(keyaEncoderRaw,
                                        keyaZeroTicks,
                                        wasZeroDone,
                                        keyaAutoZeroRuntime);
          azCorrAccum = keyaAutoZeroRuntime.corrAccum;
          Serial.print("[AZ] Zero forced from AOG - zeroTicks=");
          Serial.println(keyaZeroTicks);
        }

        // Re-Init steer settings
        // steerSettingsInit();
      }

      else if (autoSteerUdpData[3] == 0xFB) // 251 FB - SteerConfig
      {
        uint8_t sett = autoSteerUdpData[5]; // setting0

        if (bitRead(sett, 0))
          steerConfig.InvertWAS = 1;
        else
          steerConfig.InvertWAS = 0;
        if (bitRead(sett, 1))
          steerConfig.IsRelayActiveHigh = 1;
        else
          steerConfig.IsRelayActiveHigh = 0;
        if (bitRead(sett, 2))
          steerConfig.MotorDriveDirection = 1;
        else
          steerConfig.MotorDriveDirection = 0;
        if (bitRead(sett, 3))
          steerConfig.SingleInputWAS = 1;
        else
          steerConfig.SingleInputWAS = 0;
        if (bitRead(sett, 4))
          steerConfig.CytronDriver = 1;
        else
          steerConfig.CytronDriver = 0;
        if (bitRead(sett, 5))
          steerConfig.SteerSwitch = 1;
        else
          steerConfig.SteerSwitch = 0;
        if (bitRead(sett, 6))
          steerConfig.SteerButton = 1;
        else
          steerConfig.SteerButton = 0;
        if (bitRead(sett, 7))
          steerConfig.ShaftEncoder = 1;
        else
          steerConfig.ShaftEncoder = 0;

        steerConfig.PulseCountMax = autoSteerUdpData[6];

        // was speed
        // autoSteerUdpData[7];

        sett = autoSteerUdpData[8]; // setting1 - Danfoss valve etc

        steerConfig.IsDanfoss = 1;

        if (bitRead(sett, 1))
          steerConfig.PressureSensor = 1;
        else
          steerConfig.PressureSensor = 0;
        if (bitRead(sett, 2))
          steerConfig.CurrentSensor = 1;
        else
          steerConfig.CurrentSensor = 0;
        if (bitRead(sett, 3))
          steerConfig.IsUseY_Axis = 1;
        else
          steerConfig.IsUseY_Axis = 0;

        // crc
        // autoSteerUdpData[13];

        EEPROM.put(40, steerConfig);

      } // end FB
      else if (autoSteerUdpData[3] == 200) // Hello from AgIO
      {
        if (Autosteer_running)
        {
          int16_t sa = (int16_t)(steerAngleActual * 100);

          helloFromAutoSteer[5] = (uint8_t)sa;
          helloFromAutoSteer[6] = sa >> 8;

          helloFromAutoSteer[7] = (uint8_t)helloSteerPosition;
          helloFromAutoSteer[8] = helloSteerPosition >> 8;
          helloFromAutoSteer[9] = switchByte;

          SendUdp(helloFromAutoSteer, sizeof(helloFromAutoSteer), Eth_ipDestination, portDestination);
        }
        if (useBNO08x || useTM171)
        {
          SendUdp(helloFromIMU, sizeof(helloFromIMU), Eth_ipDestination, portDestination);
        }
        // SendUdp(helloFromMachine, sizeof(helloFromMachine), Eth_ipDestination, portDestination);
        // sendHelloToAgIO();
        helloFromMachine[5] = relay;
        helloFromMachine[6] = relayHi;

        int16_t CK_A = 0;
        for (uint8_t i = 2; i < sizeof(helloFromMachine) - 1; i++)
        {
          CK_A = (CK_A + helloFromMachine[i]);
        }

        if (sectionout == 1)
        {
          helloFromMachine[sizeof(helloFromMachine) - 1] = CK_A;
          SendUdp(helloFromMachine, sizeof(helloFromMachine), Eth_ipDestination, portDestination);
        }
      }

      else if (autoSteerUdpData[3] == 201)
      {
        // make really sure this is the subnet pgn
        if (autoSteerUdpData[4] == 5 && autoSteerUdpData[5] == 201 && autoSteerUdpData[6] == 201)
        {
          networkAddress.ipOne = autoSteerUdpData[7];
          networkAddress.ipTwo = autoSteerUdpData[8];
          networkAddress.ipThree = autoSteerUdpData[9];

          // save in EEPROM and restart
          EEPROM.put(60, networkAddress);
          SCB_AIRCR = 0x05FA0004; // Teensy Reset
        }
      } // end 201

      // whoami
      else if (autoSteerUdpData[3] == 202)
      {
        // make really sure this is the reply pgn
        if (autoSteerUdpData[4] == 3 && autoSteerUdpData[5] == 202 && autoSteerUdpData[6] == 202)
        {
          IPAddress rem_ip = Eth_udpAutoSteer.remoteIP();

          // hello from AgIO
          uint8_t scanReply[] = {128, 129, Eth_myip[3], 203, 7,
                                 Eth_myip[0], Eth_myip[1], Eth_myip[2], Eth_myip[3],
                                 rem_ip[0], rem_ip[1], rem_ip[2], 23};

          // checksum
          int16_t CK_A = 0;
          for (uint8_t i = 2; i < sizeof(scanReply) - 1; i++)
          {
            CK_A = (CK_A + scanReply[i]);
          }
          scanReply[sizeof(scanReply) - 1] = CK_A;

          static uint8_t ipDest[] = {255, 255, 255, 255};
          uint16_t portDest = 9999; // AOG port that listens

          // off to AOG
          SendUdp(scanReply, sizeof(scanReply), ipDest, portDest);
        }
      }
      else if (autoSteerUdpData[3] == 236) // Relay Pin Settings added by FlorianT
      {
        updatePinMapping(autoSteerUdpData);
      }
      else if (autoSteerUdpData[3] == 238)
      {
        aogConfig.raiseTime = autoSteerUdpData[5];
        aogConfig.lowerTime = autoSteerUdpData[6];
        // aogConfig.enableToolLift = autoSteerUdpData[7];

        // set1
        uint8_t sett = autoSteerUdpData[8]; // setting0
        if (bitRead(sett, 0))
          aogConfig.isRelayActiveHigh = 1;
        else
          aogConfig.isRelayActiveHigh = 0;
        if (bitRead(sett, 1))
          aogConfig.enableToolLift = 1;
        else
          aogConfig.enableToolLift = 0;

        // crc

        // save in EEPROM and restart
        EEPROM.put(70, aogConfig);
        // resetFunc();
        isHeaderFound = isPGNFound = false;
        pgn = dataLength = 0;
      }
      else if (autoSteerUdpData[3] == 0xEF && Autosteer_running) // 239
      {
        // digitalWrite(PWM2_RPWM, HIGH);
        handlePGN239(autoSteerUdpData); // Everything is handled in SectionControl.cpp
        // reset for next pgn sentence
        isHeaderFound = isPGNFound = false;
        pgn = dataLength = 0;
        watchdogKeya = 0;
      }
    } // end if 80 81 7F
  }
}
#endif

#ifdef ARDUINO_TEENSY41
void SendUdp(uint8_t *data, uint8_t datalen, IPAddress dip, uint16_t dport)
{
  Eth_udpAutoSteer.beginPacket(dip, dport);
  Eth_udpAutoSteer.write(data, datalen);
  Eth_udpAutoSteer.endPacket();
}
#endif

// ISR Steering Wheel Encoder
void EncoderFunc()
{
  if (encEnable)
  {
    pulseCount++;
    encEnable = false;
  }
}

// Hitch Control------------------------------------------------------------
void SetRelaysFendt(void)
{
  uint32_t currentMillis = millis();

  if (currentMillis - lastpushbutton >= 300)
  {
    if (goDown)
      liftGo();
    if (endDown)
      liftEnd();
  }

  // If Invert Relays is selected in hitch settings, Section 1 is used as trigger.
  if (aogConfig.isRelayActiveHigh == 1)
  {
    bitState = (bitRead(relay, 0));
  }
  // If not selected hitch command is used on headland used as Trigger
  else
  {
    if (hydLift == 1)
    {
      bitState = 1;
    }
    if (hydLift == 2)
    {
      bitState = 0;
    }
  }
  if (aogConfig.enableToolLift == 1)
  {
    if (bitState && !bitStateOld)
    {
      pressGo();
      lastpushbutton = currentMillis;
    }
    if (!bitState && bitStateOld)
    {
      pressEnd(); // Press End button - CAN Page
      lastpushbutton = currentMillis;
    }
  }
  bitStateOld = bitState;
}
